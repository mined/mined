# remove comments
/^;/ d
s,)[ 	]*;.*,),


# pre-transform ("code", ?ch) or ("code", ?\ch)
s/" ?\(\\.[^)]*\))/" "\1")/g
s/" ?\([^)]*\))/" "\1")/g

# join ("ban" ("åŠè¾¦åŠèˆ¬æ¿ç­æ¬ä¼´ç‰ˆæ‹Œç“£æ‰®æ‰³é¢é ’æ–‘ç»Šè¾¨é˜ªå‚é’£ç˜¢ç™èˆ¨å§…çµ†éˆ‘é—†è¾¯æ€‘"
#         "æ”½æ˜„ç²„æ¹´æ–’é‰¡è‚é½é­¬è¤©è™¨åŸ¿å²…æœŒç§šè‚¦èŒè¾¬ãš˜ã©¯ãªµã¸ãºœä‰½ä•°ä¬³"))
: checkjoin
/".*"$/ b join
b nojoin
: join
s,$,,
N
s,"[^"]*",,
b checkjoin

: nojoin

# detect ("arf" ("ç…§ç…¦"))
/(".* ("/ b choice

# detect ("uai"  (("uai"  "uÄi"  "uÃ¡i"  "uÇi"  "uÃ i" )))
/(".* (("/ b words

b simple


# ("arfx" ("ç…§ç…¦ä¸­äººåŠäº¬"))
# -> arfx	ç…§ ç…¦ ä¸­ äºº åŠ äº¬
: choice
s,.*("\([^"]*\)" *("\([^"]*\)")).*,\1	\2,
h
s,.*	,,
s,\(.\),\1 ,g
s, $,,
x
s,	.*,	,
G
s,	.,	,
b generate


# ("uai"  (("uai"  "uÄi"  "uÃ¡i"  "uÇi"  "uÃ i" )))
# -> uai	uai uÄi uÃ¡i uÇi uÃ i
: words
s,.*("\([^"]*\)" *((\([^)]*\)))).*,\1	\2,
s,",,g
s,   *, ,g
s, $,,
b generate


: simple

# ("code", "seq")
# -> code	seq
s,.*("\(.*\)"  *"\(.*\)").*,\1	\2,


: generate

# generate C table entry
s/\(.*\)	\(.*\)/	{"\1", "\2"},/

# drop unmatching lines
/	/ b
d
